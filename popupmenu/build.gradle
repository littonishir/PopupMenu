apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.2"


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.1.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:recyclerview-v7:26.1.0'
}

// 需要添加以下两个插件，用来编译下面的代码，这点很重要
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 这个version是区分library版本的，因此当我们需要更新library时记得修改这个version
version = "1.0.0"
// 项目的主页，项目有提交到github 就可以把 这项写上去，没有的话也可以不用，不过下
//面有引用到的 “siteUrl ” 也要对应的去掉
def siteUrl = 'https://github.com/xxx/xxx'
// Git仓库的url，项目有提交到github 就可以把 这项写上去，没有的话也可以不用，不过下面有引用到的 “siteUrl ” 也要对应的去掉
def gitUrl = 'https://github.com/xxx/xxx.git'


// Maven Group ID for the artifact，一般填你唯一的包名
//比如 compile 'com.android.support:support-v4:24.0.0' 中的
//com.android.support 部分

group = "com.littonishir.popupmenu"
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'popupmenu'
                description 'a popup menu for android.'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'littonishir'
                        name 'littonishir'
                        email 'littonishir@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false
}
// 生成javaDoc的jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//上传传到Jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        userOrg= "littonishir"
        name = "popupmenu"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
